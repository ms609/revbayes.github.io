################################################################################
#
# RevBayes Example: Inferring ancestral states and rates of morphological evolution using a hidden rates model.
#
# authors:  Sebastian HÃ¶hna
#
################################################################################

#######################
# Reading in the Data #
#######################

#CHARACTER = "solitariness"
#NUM_STATES = 2
#NUM_HIDDEN_STATES = 2

CHARACTER = args[1]
NUM_STATES = args[2]
NUM_HIDDEN_STATES = args[2]

# Import the morphological character matrix #
morpho <- readDiscreteCharacterData("data/primates_"+CHARACTER+".nex")

# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()

##############
# Tree model #
##############

# Here we use a fixed tree topology of primates
# Note that the readTrees function always returns a vector of trees,
# so we simple take the first tree
phylogeny <- readTrees("data/primates_tree.nex")[1]


#########################
# Set up the rate model #
#########################

# we assume every rate i <-> j to be independent and exponentially distributed
rate_pr := phylogeny.treeLength() / 10
NUM_RATES = NUM_STATES * (NUM_STATES-1)
for ( i in 1:NUM_RATES ) {
    obs_rate[i] ~ dnExp(rate_pr)
    moves.append( mvScale( obs_rate[i], weight=2 ) )
}


NUM_HIDDEN_RATES = NUM_HIDDEN_STATES * (NUM_HIDDEN_STATES-1)
for ( i in 1:NUM_HIDDEN_RATES ) {
    hidden_rate[i] ~ dnExp(rate_pr)
    moves.append( mvScale( hidden_rate[i], weight=2 ) )
}

##########################
# Set up the rate matrix #
##########################

Q_morpho := fnHiddenStateRateMatrix( obs_rate, hidden_rate, rescale=false )



#####################################
# Set up the root state frequencies #
#####################################

rf_prior <- rep(1,NUM_STATES*NUM_HIDDEN_STATES)
rf ~ dnDirichlet( rf_prior )
moves.append( mvBetaSimplex( rf, weight=2 ) )
moves.append( mvDirichletSimplex( rf, weight=2 ) )



###################
# Set up the CTMC #
###################

# Combine all of our elements into a CTMC.

phyMorpho ~ dnPhyloCTMC(tree=phylogeny, Q=Q_morpho, rootFrequencies=rf, type="NaturalNumbers")

morpho_exp = morpho.expandCharacters( NUM_HIDDEN_STATES )
phyMorpho.clamp(morpho_exp)



########
# MCMC #
########

# initialize the model object #
mymodel = model(phylogeny)
# Create a vector of monitors #
# 1. for the full model #
monitors.append( mnModel(filename="output/"+CHARACTER+"_hrm.log", printgen=1) )
# 2. and a few select parameters to be printed to the screen #
monitors.append( mnScreen(printgen=10) )
# 3. add an ancestral state monitor
monitors.append( mnJointConditionalAncestralState(tree=phylogeny,
                                                  ctmc=phyMorpho,
                                                  filename="output/"+CHARACTER+"_hrm.states.txt",
                                                  type="NaturalNumbers",
                                                  printgen=1,
                                                  withTips=true,
                                                  withStartStates=false) )

# Initialize the MCMC object #
mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")

# Run the MCMC #
mymcmc.run(generations=5000, tuningInterval=200)

# check the performance of the MCMC/moves
mymcmc.operatorSummary()


# Read in the tree trace and construct the ancestral states (ASE) #
anc_states = readAncestralStateTrace("output/"+CHARACTER+"_hrm.states.txt")
anc_tree = ancestralStateTree(tree=phylogeny, ancestral_state_trace_vector=anc_states, include_start_states=false, file="output/"+CHARACTER+"_ase_hrm.tree", burnin=0.25, summary_statistic="MAP", site=1, nStates=NUM_STATES*NUM_HIDDEN_STATES)

# Quit RevBayes #
q()
